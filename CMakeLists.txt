CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(libmultifinder)

# parameters
OPTION(BUILD_STATIC "Build static libraries" ON)
OPTION(BUILD_SHARED "Build shared libraries" ON)
OPTION(BUILD_TOOLS "Build tools" ON)

# conditions
IF(NOT BUILD_STATIC AND NOT BUILD_SHARED)
  MESSAGE(FATAL_ERROR "Cannot build with both BUILD_STATIC and BUILD_SHARED disabled")
ENDIF()

# Doxygen
FIND_PACKAGE(Doxygen)
OPTION(BUILD_DOCUMENTATION "Create and install API documentation (requires Doxygen)" ${DOXYGEN_FOUND})

# build parameters
SET(CMAKE_C_FLAGS "-Wall")

INCLUDE_DIRECTORIES(include)

# build definitions
SET(ALLTARGETS)
SET(LINKTYPES)
IF(BUILD_STATIC)
  LIST(APPEND LINKTYPES "STATIC")
ENDIF()
IF(BUILD_SHARED)
  LIST(APPEND LINKTYPES "SHARED")
ENDIF()

FOREACH(LINKTYPE ${LINKTYPES})
  ADD_LIBRARY(multifinder_${LINKTYPE} ${LINKTYPE} lib/multifinder.c)
  SET_TARGET_PROPERTIES(multifinder_${LINKTYPE} PROPERTIES DEFINE_SYMBOL "BUILD_MULTIFINDER_DLL")
  SET_TARGET_PROPERTIES(multifinder_${LINKTYPE} PROPERTIES COMPILE_DEFINITIONS "${LINKTYPE}")
  SET_TARGET_PROPERTIES(multifinder_${LINKTYPE} PROPERTIES OUTPUT_NAME multifinder)
  TARGET_INCLUDE_DIRECTORIES(multifinder_${LINKTYPE} PRIVATE lib)
  #TARGET_LINK_LIBRARIES(multifinder_${LINKTYPE} ${ANYZIP_LIBRARIES} ${EXPAT_LIBRARIES})
  SET(ALLTARGETS ${ALLTARGETS} multifinder_${LINKTYPE})

  SET(EXELINKTYPE ${LINKTYPE})
ENDFOREACH()

IF(BUILD_TOOLS)
  ADD_EXECUTABLE(multifinder_count src/multifinder_count.c)
  TARGET_LINK_LIBRARIES(multifinder_count multifinder_${EXELINKTYPE})
  LIST(APPEND ALLTARGETS multifinder_count)
  ADD_EXECUTABLE(multifinder_replace src/multifinder_replace.c)
  TARGET_LINK_LIBRARIES(multifinder_replace multifinder_${EXELINKTYPE})
  LIST(APPEND ALLTARGETS multifinder_replace)
ENDIF()

IF(BUILD_DOCUMENTATION)
  IF(NOT DOXYGEN_FOUND)
    MESSAGE(FATAL_ERROR "Doxygen is needed to build the documentation.")
  ENDIF()
  ADD_CUSTOM_TARGET(doc ALL
    COMMAND ${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_SOURCE_DIR}/doc/Doxyfile
    #WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    COMMENT "Generating API documentation with Doxygen"
    VERBATIM
  )
  INSTALL(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/doc/man
    DESTINATION .
  )
  #INSTALL(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/doc/html
  #  DESTINATION share/doc
  #)
ENDIF()

# installation specifications
INSTALL(TARGETS ${ALLTARGETS}
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib
  RUNTIME DESTINATION bin
)
INSTALL(DIRECTORY include/
  DESTINATION include 
  FILES_MATCHING PATTERN "multifinder*.h"
)
